palette("#C38154", "#5e3327", "#FFC26F")
form {
	let end = easeInOut(@time % 5000 / 5000 * 2)
	construct(#cafetera) {
		let nodeRad = 0.07
		let lineRad = 0.03
		default node(radius: nodeRad)
		default line(radius: lineRad, progressEnd: end)
		node(color: 2, point: <y: 3>, radius: 0.5)
		var baseHeight = 4.4
		let baseRad = 3
		// Bottom bit
		refract(center: <0, 0, 0>, count: 7) {
			rotate(center: <0, 0, 0>, axis: <1, 0, 0>, angle: 8) {
				translate(z: -1 * baseRad) {
					let sliceRad = 1.13
					let sliceMod = 0.3
					node(#a, <x: -1 * sliceRad - sliceMod>)
					node(#b, <x: -1 * sliceRad, y: baseHeight>)
					node(#c, <x: sliceRad, y: baseHeight>)
					node(#d, <x: sliceRad + sliceMod>)
					line(points: [#a, #b])
					line(points: [#a, <-0.75, -0.2>, <0, -0.3>, <0.75, -0.2>, #d], curve: true)
					line(points: [#b, <-0.75, baseHeight + 0.2>, <0, baseHeight + 0.3>, <0.75, baseHeight + 0.2>, #c], curve: true)
				}
			}
		}
		drawCircle(center: [0, 0.15, 0], radius: baseRad * 1.03, count: 30)
		baseHeight = baseHeight + 0.125
		// Middle bit
		let midRad = 2.25
		let bottom = drawCircle(center: [0, baseHeight + 0.5, 0], radius: midRad, count: 10)
		let top = drawCircle(center: [0, baseHeight + 1.5, 0], radius: midRad, count: 10)
		for i in 0 to bottom.length() - 1 {
			line(points: [bottom[i], top[i]])
		}
		// Top bit
		let topRad = baseRad
		let topStart = baseHeight + 2
		let topHeight = baseHeight + 0.3
		refract(center: <0, 0, 0>, count: 7) {
			rotate(center: <0, topStart + topHeight / 2, 0>, axis: <1, 0, 0>, angle: -8) {
				translate(z: -1 * topRad * 0.9) {
					let sliceRad = 1.13
					let sliceMod = 0.325
					node(#e, <x: -1 * sliceRad, y: topStart>)
					node(#f, <x: -1 * sliceRad - sliceMod, y: topStart + topHeight>)
					node(#g, <x: sliceRad + sliceMod, y: topStart + topHeight>)
					node(#h, <x: sliceRad, y: topStart>)
					line(points: [#e, #f, #g, #h])
					line(points: [#e, <-0.75, topStart - 0.2>, <0, topStart - 0.3>, <0.75, topStart - 0.2>, #h], curve: true)
					node(#top, point: <y: topStart + topHeight + 0.3, z: topRad + 0.04>)
					line(points: [#f, #top, #g])
				}
			}
		}
		// Handle
		let handleColor = 1
		let handleNodeRad = 0
		rotate(center: <0, 0, 0>, axis: <0, 1, 0>, angle: -17) {
			translate(y: -0.8) {
				refract(center: <0, 0, 0>, count: 2, angle: 8) {
					default node(radius: handleNodeRad, color: handleColor)
					default line(color: handleColor)
					node(#i, <x: topRad * 1.1, y: topStart + topHeight - 0.1, z: 0>)
					node(#j, <x: 4.5, y: topStart + topHeight + 0.05, z: 0>)
					node(#k, <x: 5.5, y: topStart + topHeight - 1, z: 0>)
					node(#l, <x: 4.5, y: topStart + topHeight - 4, z: 0>)
					node(#m, <x: 3.8, y: topStart + topHeight - 4, z: 0>)
					node(#n, <x: 4.6, y: topStart + topHeight - 1.4, z: 0>)
					node(#o, <x: 4.2, y: topStart + topHeight - 0.8, z: 0>)
					node(#p, <x: 3.2, y: topStart + topHeight - 0.8, z: 0>)
					line(points: [#p, #i, #j, #k, #l, #m], radius: lineRad)
					line(points: [#m, #n, #o, #p], curve: true, radius: lineRad)
				}
			}
		}
		// Spout
		let spoutNodeRad = 0
		rotate(center: <0, 0, 0>, axis: <0, 1, 0>, angle: -103) {
			translate(y: baseHeight + topHeight + 0.6, z: topRad) {
				default node(radius: spoutNodeRad)
				let width = 1
				node(#q, <y: -1.5>)
				node(#r, <x: -1 * width, y: 1>)
				node(#s, <x: width, y: 1>)
				node(#t, <y: 0.8, z: 1.45>)
				node(#u, <y: 0.35, z: 0.95>)
				node(#v, <x: -1 * width / 3.1415, y: 1, z: 1>)
				node(#w, <x: width / 3.1415, y: 1, z: 1>)
				line(points: [#r, #v, #t])
				line(points: [#s, #w, #t])
				line(points: [#u, #q])
				line(points: [#u, #t])
				line(points: [#r, #q, #s])
				line(points: [#v, #u, #w])
			}
		}
		// Top handle
		let handleHeight = 1.45
		let handleSides = 6
		translate(y: topStart + topHeight + 0.3) {
			node(#handle-top, <y: handleHeight>)
			let handleNodes = drawCircle(center: [0, handleHeight - 0.3, 0], radius: 0.6, count: handleSides, connect: true)
			for i in 0 to handleNodes.length() - 1 {
				line(points: [#handle-top, handleNodes[i]])
			}
			let bottomHandleNodes = drawCircle(center: [0, 0, 0], radius: 0.3, count: handleSides, connect: true)
			for i in 0 to bottomHandleNodes.length() - 1 {
				line(points: [bottomHandleNodes[i], handleNodes[i]])
			}
		}
	}
	rotate(#cafetera, center: <0, 0, 0>, angle: easeInOut((@time + 2500) % 5000 / 5000) * 360 - 180)
}

func drawCircle(center: [Number], radius: Number, count: Number, angleOffset: Number = 0, connect: Boolean = true): [NodeIdentifier] {
	// No longer necessary with built in functions
	let angle = 3.1415 * 2 / count
	var nodes: [NodeIdentifier]
	for i in 0 to count {
		let x = center[0] + radius * cos(angle * i + angleOffset)
		let z = center[2] + radius * sin(angle * i + angleOffset)
		let point = node(point: <x: x, y: center[1], z: z>, radius: 0.01)
		nodes.add(point)
	}
	if connect {
		line(points: nodes)
	}
	return nodes
}

func easeInOut(x: Number): Number {
	return -1 * (cos(3.1415 * x) - 1) / 2
}